# [Power System Model](@id PowerSystemModelManual)

The JuliaGrid supports the composite type `PowerSystem` to preserve power system data, with the following fields: `bus`, `branch`, `generator`, `base`, `acModel`, and `dcModel`. The fields `bus`, `branch`, and `generator` hold data related to buses, branches, and generators, respectively. The `base` field stores base values for power and voltages, with the default being three-phase power measured in volt-amperes for the base power and line-to-line voltages measured in volts for base voltages. The `acModel` and `dcModel` store vectors and matrices that are related to the topology and parameters of the power system.

The composite type `PowerSystem` can be created using a function:
* [`powerSystem`](@ref powerSystem).
JuliaGrid supports three modes for populating the `PowerSystem` type: using built-in functions, using HDF5 file format, and using [Matpower](https://matpower.org) case files.

It is recommended to use the HDF5 format for large-scale systems. To facilitate this, JuliaGrid has the function:
* [`savePowerSystem`](@ref savePowerSystem).

Once the `PowerSystem` type is created, you can add buses, branches, and generators using the following functions:
* [`addBus!`](@ref addBus!),
* [`addBranch!`](@ref addBranch!),
* [`addGenerator!`](@ref addGenerator!).
Additionally, JuliaGrid provides macros [`@bus`](@ref @bus), [`@branch`](@ref @branch), and [`@generator`](@ref @generator) to define templates that aid in creating buses, branches, and generators. These templates help avoid entering the same parameters repeatedly.

To create vectors and matrices based on the power system topology and parameters, you can use the following functions:
* [`acModel!`](@ref acModel!),
* [`dcModel!`](@ref dcModel!).

In addition, it is possible to manipulate the parameters of buses, branches, and generators using the following functions:
* [`shuntBus!`](@ref shuntBus!),
* [`statusBranch!`](@ref statusBranch!),
* [`parameterBranch!`](@ref parameterBranch!),
* [`statusGenerator!`](@ref statusGenerator!),
* [`outputGenerator!`](@ref outputGenerator!).
Executing these functions will automatically update all fields affected by them within `PowerSystem` composite type. You can also change other parameters of the power system by accessing and modifying the values in the `bus`, `branch`, `generator`, and `base` fields of the `PowerSystem` type.

Finally, after adding the generator, JualiGrid provides the following functions to incorporate costs for the active and reactive power generated by the generator:
* [`addActiveCost!`](@ref addActiveCost!),
* [`addReactiveCost!`](@ref addReactiveCost!).

---

## [Build Model](@id BuildModelManual)
The [`powerSystem`](@ref powerSystem) function generates the `PowerSystem` composite type and requires a string-formatted path to either Matpower case or HDF5 files as input. Alternatively, the `PowerSystem` can be created without any initial data by initializing it as empty, allowing the user to construct the power system from scratch.

---

##### Matpower or HDF5 File
For example, to create the `PowerSystem` type using the Matpower case file for the IEEE 14-bus test case, which is named `case14.m` and located in the folder `C:\matpower`, the following Julia code can be used:
```julia
system = powerSystem("C:/matpower/case14.m")
```

In order to use the HDF5 file as input to create the `PowerSystem` object, it is necessary to have saved the data using the [`savePowerSystem`](@ref savePowerSystem) function beforehand. As an example, let us say we saved the power system as `case14.h5` in the directory `C:\hdf5`. In this case, the following Julia code can be used to construct the `PowerSystem` composite type:
```julia
system = powerSystem("C:/hdf5/case14.h5")
```

!!! tip "Tip"
    It is recommended to load the power system from the HDF5 file to reduce the loading time.

---

##### Model from Scratch
Alternatively, the model can be build from the scratch using built-in functions, for example:
```@example buildModelScratch
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1, base = 345e3)
addBus!(system; label = 2, type = 1, reactive = 0.05, base = 345e3)

addBranch!(system; label = 1, from = 1, to = 2, resistance = 0.008, reactance = 0.05)
```

---

##### Internal Unit System
The `PowerSystem` composite type stores all electrical quantities in per-units and radians, except for the base values of power and voltages. The base power value is expressed in volt-amperes, while the base voltages are given in volts.

---

##### Change Internal Base Unit Prefixes
As an example, if you execute the previous code snippet, you can retrieve the base power and base voltage values and units as shown below:
```@repl buildModelScratch
system.base.power.value, system.base.power.unit
system.base.voltage.value, system.base.voltage.unit
```

By using the [`@base`](@ref @base) macro, users can change the prefixes of the base units. For instance, if the user wishes to convert base power and base voltage values to megavolt-amperes (MVA) and kilovolts (kV) respectively, they can execute the following macro:
```@example buildModelScratch
@base(system, MVA, kV)
nothing # hide
```
Upon execution of the macro, the base power and voltage values and units will be modified accordingly:
```@repl buildModelScratch
system.base.power.value, system.base.power.unit
system.base.voltage.value, system.base.voltage.unit
```

Therefore, by using the [`@base`](@ref @base) macro to modify the prefixes of the base units, users can convert the output data from various analyses to specific units with the desired prefixes.

---

## [Save Model](@id SaveModelManual)
Once the `PowerSystem` type has been created using one of the methods outlined in [Build Model](@ref BuildModelManual), the current data can be stored in the HDF5 file by using  [`savePowerSystem`](@ref savePowerSystem) function:
```julia
savePowerSystem(system; path = "C:/matpower/case14.h5", reference = "IEEE 14-bus test case")
```
All electrical quantities saved in the HDF5 file are in per-units and radians, except for base values for power and voltages, which are given in volt-amperes and volts. It is important to note that even if the user modifies the base units using the [`@base`](@ref @base) macro, the units will still be saved in the default settings.

---

## [Add Bus](@id AddBusManual)
We have the option to add buses to a loaded power system or to one created from scratch. As an illustration, we can initiate the `PowerSystem` type and then incorporate two buses by utilizing the [`addBus!`](@ref addBus!) function:
```@example addBus
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1, base = 345e3)
addBus!(system; label = 2, type = 1, angle = -0.034907, base = 345e3)
```

In this case, we have created two buses where the active power demanded by the consumer at buses labelled with 1 is specified in per-units, which are the same units used to store electrical quantities:
```@repl addBus
system.bus.demand.active
```

In addition, it is worth noting that the `base` keyword is used to specify the base voltages, and its default input unit is in volts (V).
```@repl addBus
system.base.voltage.value, system.base.voltage.unit
```

Finally, we set the bus voltage angle in radians for the bus with label 2 to its initial value:
```@repl addBus
system.bus.voltage.angle
```

!!! note "Info"
    We recommend reading the documentation for the [`addBus!`](@ref addBus!) function, where all the keywords used in the function are explained in detail.

---

##### Change Input Unit System
Typically, all keywords associated with electrical quantities are expected to be provided in per-units (pu) and radians (rad) by default, with the exception of base voltages, which should be specified in volts (V). However, users can choose to use different units than the default per-units and radians or modify the prefix of the base voltage unit by using macros such as the following:
```@example addBusUnit
using JuliaGrid # hide

@power(MW, MVAr, MVA)
@voltage(pu, deg, kV)
nothing # hide
```

We can create identical two buses as before using new system of units as follows:
```@example addBusUnit
system = powerSystem()

addBus!(system; label = 1, type = 3, active = 10.0, base = 345.0)
addBus!(system; label = 2, type = 1, angle = -2.0, base = 345.0)
```

As can be observed, electrical quantities will continue to be stored in per-units and radians format:
```@repl addBusUnit
system.bus.demand.active
system.bus.voltage.angle
```

The base voltage values will still be stored in volts (V) since we only changed the input unit prefix, and did not modify the internal unit prefix, as shown below:
```@repl addBusUnit
system.base.voltage.value, system.base.voltage.unit
```
To modify the internal unit prefix, the following macro can be used:
```@example addBusUnit
@base(system, VA, kV)
nothing # hide
```
After executing this macro, the base voltage values will be stored in kilovolts (kV):
```@repl addBusUnit
system.base.voltage.value, system.base.voltage.unit
```

---

## [Bus Labels](@id BusLabelsManual)
In JuliaGrid, the set of bus labels is always internally assigned and stored in a dictionary, even if the labels assigned to the buses are in an increasing ordered set of integers. In this scenario, each bus label value corresponds to the same internally assigned value. As a result, the user can omit the `label` keyword. For instance:
```@example BusLabels
using JuliaGrid # hide

system = powerSystem()

addBus!(system)
addBus!(system)

nothing # hide
```
These bus labels, which are also internally assigned, are stored in the variable:
```@repl BusLabels
system.bus.label
```

However, if the labels assigned to the buses are not in an increasing ordered set of integers, the system will always internally renumber all labels to satisfy the requirement of an increasing ordered set of integers. For example, suppose we have a power system with buses labelled as:
```@example AccessBusLabels
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 30)
addBus!(system; label = 20)

nothing # hide
```

These labels, along with their internally assigned labels, are stored in the variable:
```@repl AccessBusLabels
system.bus.label
```

If a user needs to retrieve the original labels and match them with other parameters related to the buses, they can do so using the following code:
```@repl AccessBusLabels
label = collect(keys(sort(system.bus.label; byvalue = true)))
```

---

## [Add Branch](@id AddBranchManual)
After adding buses with unique labels, we can define branches between them. The branch cannot be added unless the buses are already defined, and the `from` and `to` keywords should correspond to the already defined bus labels. For instance:
```@example addBranch
using JuliaGrid # hide
@default(unit)  # hide

system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1)
addBus!(system; label = 2, type = 1, angle = -0.034907)

addBranch!(system; label = 1, from = 1, to = 2, reactance = 0.12, shiftAngle = 0.1745)
```
Here, we created branch from bus 1 to bus 2 with following parameters:
```@repl addBranch
system.branch.parameter.reactance
system.branch.parameter.shiftAngle
```
!!! note "Info"
    It is recommended to consult the documentation for the [`addBranch!`](@ref addBranch!) function, which provides an explanation of all the keywords used in the function.

---

##### Change Input Unit System
To use units other than per-units (pu) and radians (rad), macros can be employed to change the input units. For example, if the need arises to use ohms (Ω) and degrees (deg), the macros below can be employed:
```@example addBranchUnit
using JuliaGrid # hide
@parameter(Ω, pu)
@voltage(pu, deg, V)

system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1)
addBus!(system; label = 2, type = 1, angle = -2.0)

addBranch!(system; label = 1, from = 1, to = 2, reactance = 22.8528, shiftAngle = 10.0)
```
Still, all electrical quantities are stored in per-units and radians, and the same branch as before is created, as shown in the following output:
```@repl addBranchUnit
system.branch.parameter.reactance
system.branch.parameter.shiftAngle
```

It is important to note that, when working with impedance and admittance values in ohms (Ω) and siemens (S) that are related to a transformer, the assignment must be based on the primary side of the transformer.

---

## [Branch Labels](@id BranchLabelsManual)
If the branch labels are not in an ordered set of increasing integers, the system will internally renumber all labels, similar to how [bus lables](@ref BusLabelsManual) are handled. For example, consider a power system with non-ordered bus and branch labels as shown below:
```@example AccessBranchLabels
using JuliaGrid # hide
@default(unit) # hide

system = powerSystem()

addBus!(system; label = 30)
addBus!(system; label = 20)
addBus!(system; label = 40)

addBranch!(system; label = 100, from = 30, to = 20, reactance = 0.8)
addBranch!(system; label = 200, from = 20, to = 40, reactance = 0.5)

nothing # hide
```

The system stores the original branch labels along with their internally assigned labels:
```@repl AccessBranchLabels
system.branch.label
```
To retrieve the original labels and match them with other branch parameters, users can use the following code:
```@repl AccessBranchLabels
label = collect(keys(sort(system.branch.label; byvalue = true)))
```

Furthermore, the `from` and `to` keywords are saved based on the internally assigned numerical values, which are derived from the increasing ordered set of integers based on bus labels:
```@repl AccessBranchLabels
system.bus.label
[system.branch.layout.from system.branch.layout.to]
```
To retrieve the original `from` and `to` labels, the following approach can be used:
```@repl AccessBranchLabels
labelBus = collect(keys(sort(system.bus.label; byvalue = true)))
[labelBus[system.branch.layout.from] labelBus[system.branch.layout.to]]
```

Also, users have the option to omit the `label` keyword when using the [`addBranch!`](@ref addBranch!) function. In this case, the system internally assigns labels in an increasing order.

---

## [Add Generator](@id AddGeneratorManual)
After defining the buses, generators can be added to the power system. Each generator must have a unique label, and the `bus` keyword should correspond to the unique label of the bus it is connected to. For instance:
```@example addGenerator
using JuliaGrid # hide
@default(unit) # hide

system = powerSystem()

addBus!(system; label = 1)
addBus!(system; label = 2)

addGenerator!(system; label = 1, bus = 2, active = 0.5, reactive = 0.1)

nothing # hide
```

In the above code, we add the generator to the bus labelled 2, with active and reactive power outputs set to:
```@repl addGenerator
system.generator.output.active
system.generator.output.reactive
```
Similar to buses and branches, the input units can be changed to units other than per-units using different macros.

!!! note "Info"
    It is recommended to refer to the documentation for the [`addGenerator!`](@ref addGenerator!) function, which explains all the keywords used in the function.

---

## [Generator Labels](@id GeneratorLabelsManual)
Similar to how the system handles [bus labels](@ref BusLabelsManual) and [branch labels](@ref BranchLabelsManual), the system will internally renumber all generator labels if they are not in an ordered set of increasing integers. As an example, let us take the power system with non-ordered bus and generator labels, as illustrated below:
```@example AccessGeneratorLabels
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 30)
addBus!(system; label = 20)
addBus!(system; label = 40)

addGenerator!(system; label = 200, bus = 40)
addGenerator!(system; label = 100, bus = 30)

nothing # hide
```

The JuliaGrid stores both the original generator labels and their corresponding internally assigned labels.
```@repl AccessGeneratorLabels
system.generator.label
```
To retrieve the original labels and match them with other generator parameters, users can utilize the following code:
```@repl AccessGeneratorLabels
label = collect(keys(sort(system.generator.label; byvalue = true)))
```

Additionally, the `bus` keyword is saved based on the internally assigned numerical values, which are derived from the ordered set of integers based on bus labels:
```@repl AccessGeneratorLabels
system.bus.label
system.generator.layout.bus
```
To obtain the original labels of the `bus` keyword, you can use the following code:
```@repl AccessGeneratorLabels
labelBus = collect(keys(sort(system.bus.label; byvalue = true)))
label = labelBus[system.generator.layout.bus]
```

Also, users have the option to omit the `label` keyword when using the [`addGenerator!`](@ref addGenerator!) function. In this case, the system internally assigns labels in an increasing order.

---

## [Add Templates](@id AddTemplatesManual)
The functions [`addBus!`](@ref addBus!), [`addBranch!`](@ref addBranch!), and [`addGenerator!`](@ref addGenerator!) are used to add bus, branch, and generator to the power system, respectively. If certain keywords are not specified, default values are assigned to some parameters.

---

##### Default Keyword Values
For the [`addBus!`](@ref addBus!) function, if the `type` keyword is not provided, the bus type is automatically set to a demand bus with a value of 1 for `type`. The initial bus voltage `magnitude` is set to 1.0 per-unit, and the `base` voltage is set to 138e3 volts. These default values are important to prevent issues with algorithm execution, such as a singular Jacobian when `magnitude = 0.0`.

The [`addBranch!`](@ref addBranch!) function assigns default values of 1 for `status`, indicating that the branch is in-service, and 1 for `type`, representing the type of ratings.

Similarly, the [`addGenerator!`](@ref addGenerator!) function assigns default values of 1 for `status`, indicating that the generator is in-service, and 1.0 per-unit for `magnitude`, which represents the voltage magnitude setpoint.

The remaining parameters are initialized with default values of zero.

---

##### Change Default Keyword Values
In JuliaGrid, users are allowed to modify default values and assign non-zero values to other keywords using the [`@bus`](@ref @bus), [`@branch`](@ref @branch), and [`@generator`](@ref @generator) macros. These macros create bus, branch, and generator templates that are used every time the [`addBus!`](@ref addBus!), [`addBranch!`](@ref addBranch!), and [`addGenerator!`](@ref addGenerator!) functions are called. For instance, the code block shows an example of creating bus, branch, and generator templates with customized default values:
```@example CreateBusTemplate
using JuliaGrid # hide
@default(unit) # hide

system = powerSystem()

@bus(type = 2, active = 0.1)
addBus!(system; label = 1)
addBus!(system; label = 2, type = 1, active = 0.5)

@branch(reactance = 0.12)
addBranch!(system; label = 1, from = 1, to = 2)
addBranch!(system; label = 2, from = 1, to = 2, reactance = 0.06)

@generator(magnitude = 1.1)
addGenerator!(system; label = 1, bus = 1, active = 50)
addGenerator!(system; label = 2, bus = 1, active = 20)

nothing # hide
```

This code example involves two uses of the [`addBus!`](@ref addBus!) and [`addBranch!`](@ref addBranch!) functions. In the first use, the functions rely on the default values set by the templates created with the [`@bus`](@ref @bus) and [`@branch`](@ref @branch) macros. In contrast, the second use passes specific values that match the keywords used in the templates. As a result, the templates are overridden:
```@repl CreateBusTemplate
system.bus.layout.type
system.bus.demand.active
system.branch.parameter.reactance
```

In the given example, the [`@generator`](@ref @generator) macro is utilized instead of repeatedly specifying the `magnitude` keyword in the [`addGenerator!`](@ref addGenerator!) function. This macro creates a generator template with a default value for `magnitude`, which is automatically applied every time the [`addGenerator!`](@ref addGenerator!) function is called. Therefore, it eliminates the requirement to set the magnitude value for each individual generator:
```@repl CreateBusTemplate
system.generator.voltage.magnitude
```
---

##### Change Input Unit System
The JuliaGrid requires users to specify electrical quantity-related keywords in per-units (pu) and radians (rad) by default. However, it provides macros, such as [`@power`](@ref @power), that allow users to specify other units:
```@example CreateBusTemplateUnits
using JuliaGrid # hide

system = powerSystem()

@power(MW, MVAr, MVA)

@bus(active = 100, reactive = 200)
addBus!(system; label = 1)

@power(pu, pu, pu)

addBus!(system; label = 2, active = 1.0, reactive = 2.0)

nothing # hide
```

In this example, we create the bus template and one bus using SI power units, and then we switch to per-units and add the second bus. It is important to note that once the template is defined in any unit system, it remains valid regardless of subsequent unit system changes. The resulting power values are:
```@repl CreateBusTemplateUnits
system.bus.demand.active
system.bus.demand.reactive
```
Thus, JuliaGrid automatically tracks the unit system used to create templates and provides the appropriate conversion to per-units and radians. Even if the user switches to a different unit system later on, the previously defined template will still be valid.

---

##### Multiple Templates
In the case of calling the [`@bus`](@ref @bus), [`@branch`](@ref @branch), or [`@generator`](@ref @generator) macros multiple times, the provided keywords and values will be combined into a single template for the corresponding component (bus, branch, or generator), which will be used for generating the component.

---

##### Reset Templates
To reset the bus, branch, and generator templates to their default settings, users can utilize the following macros:
```@example CreateBusTemplateUnits
@default(bus)
@default(branch)
@default(generator)
nothing # hide
```
Additionally, users can reset all templates for the bus, branch, and generator components using the macro:
```@example CreateBusTemplateUnits
@default(template)
nothing # hide
```

---

## [AC and DC Model](@id ACDCModelManual)
When we constructed the power system, we can create an AC and/or DC model, which include vectors and matrices related to the power system's topology and parameters. The following code snippet demonstrates this:
```@example ACDCModel
using JuliaGrid # hide
@default(unit) # hide

system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1)
addBus!(system; label = 2, type = 1, reactive = 0.05)
addBus!(system; label = 3, type = 1, susceptance = 0.05)

addBranch!(system; from = 1, to = 2, reactance = 0.12, shiftAngle = 0.1745)
addBranch!(system; from = 2, to = 3, resistance = 0.008, reactance = 0.05)

acModel!(system)
dcModel!(system)

nothing # hide
```
The nodal matrices are one of the components of both the AC and DC models and are stored in the variables:
```@repl ACDCModel
system.dcModel.nodalMatrix
system.acModel.nodalMatrix
```

!!! note "Info"
    The AC model is used for performing AC power flow, AC optimal power flow, nonlinear state estimation, or state estimation with PMUs, whereas the DC model is essential for various DC or linear analyses. Consequently, once these models are developed, they can be applied to various types of simulations. We recommend that the reader refer to the tutorial on [AC and DC models](@ref ACDCModelTutorials).

---

##### New Branch Triggers Model Update
We can execute the [`acModel!`](@ref acModel!) and [`dcModel!`](@ref dcModel!) functions after defining the final number of buses, and each new branch added will trigger an update of the AC and DC vectors and matrices. Here is an example:
```@example ACDCModelUpdate
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1)
addBus!(system; label = 2, type = 1, reactive = 0.05)
addBus!(system; label = 3, type = 1, susceptance = 0.05)

acModel!(system)
dcModel!(system)

addBranch!(system; from = 1, to = 2, reactance = 0.12, shiftAngle = 0.1745)
addBranch!(system; from = 2, to = 3, resistance = 0.008, reactance = 0.05)

nothing # hide
```
For example, the nodal matrix in the DC framework has the same values as before:
```@repl ACDCModelUpdate
system.dcModel.nodalMatrix
```

!!! tip "Tip"
    It is not fully recommended to create AC and DC models before adding a large number of branches if the execution time of functions is important. Instead, triggering updates to the AC and DC models using the [`addBranch!`](@ref addBranch!) function is useful for power systems that require the addition of several branches. This update avoids the need to recreate vectors and matrices from scratch.

---

##### New Bus Triggers Model Erasure
The AC and DC models must be defined when a finite number of buses are defined, otherwise, adding a new bus will delete them. For example, if we attempt to add a new bus to the `PowerSystem` type that was previously created, the current AC and DC models will be completely erased:
```@repl ACDCModelUpdate
addBus!(system; label = 4, type = 2)
system.dcModel.nodalMatrix
system.acModel.nodalMatrix
```

---

## [Alter Shunt Elements](@id AlterShuntElementsManual)
To modify or add new shunt element at bus, you can use the [`shuntBus!`](@ref shuntBus!) function. If the AC and DC models have not yet been created, you can directly modify the `bus.shunt` field of the `PowerSystem` type to change their values. However, if AC and DC models have been created, using the [`shuntBus!`](@ref shuntBus!) function will automatically update all relevant fields in these models. This avoids the need to recreate the AC and DC models from scratch.

Therefore, it is recommended to use this function after executing the [`acModel!`](@ref acModel!) and [`dcModel!`](@ref dcModel!) functions. For example, let us start by creating the AC model:
```@example shuntBus
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1, type = 3)
addBus!(system; label = 2, type = 1, susceptance = 2.1)

addBranch!(system; from = 1, to = 2, reactance = 0.12)

acModel!(system)

nothing # hide
```
Then, the nodal matrix is given as:
```@repl shuntBus
system.acModel.nodalMatrix
```

For example, to add a shunt element at bus 1 with a specified conductance value and modify the susceptance value of the shunt element at bus 2, we can execute the following code:
```@example shuntBus
shuntBus!(system; label = 1, conductance = 0.04)
shuntBus!(system; label = 2, susceptance = 0.25)

nothing # hide
```
Upon examining the nodal matrix, it can be inferred that the [`shuntBus!`](@ref shuntBus!) function automatically updates this matrix:
```@repl shuntBus
system.acModel.nodalMatrix
```

---

## [Change Branch Status](@id ChangeBranchStatusManual)
We can use the [`statusBranch!`](@ref statusBranch!) function to switch the branch's status between in-service and out-of-service. If the AC and DC models are not created, the function will perform the same operation as accessing the `status` variable of the `branch.layout` field and changing the value from 1 to 0 or vice versa. However, if the AC and DC models are created, the function will trigger updates to all affected vectors and matrices.

Therefore, it is recommended to use this function after executing the [`acModel!`](@ref acModel!) and [`dcModel!`](@ref dcModel!) functions. The following code demonstrates the usage of the [`statusBranch!`](@ref statusBranch!) function:
```@example statusBranch
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1)
addBus!(system; label = 2)
addBus!(system; label = 3)

addBranch!(system; label = 1, from = 1, to = 2, reactance = 0.12, shiftAngle = 0.1745)
addBranch!(system; label = 2, from = 2, to = 3, resistance = 0.008, reactance = 0.05)

acModel!(system)
dcModel!(system)

statusBranch!(system; label = 1, status = 0)

nothing # hide
```

This code sets the branch labelled with 1 to out-of-service. For example, the nodal matrix in the DC framework has the following form:
```@repl statusBranch
system.dcModel.nodalMatrix
```

---

##### Drop Zeros
After the execution of the [`statusBranch!`](@ref statusBranch!) function, the nodal matrices will contain zeros, as demonstrated in the code example. If needed, the user can remove these zeros by using the `dropzeros!` function, as shown below:
```@repl statusBranch
using SparseArrays
dropzeros!(system.dcModel.nodalMatrix)
```

!!! note "Info"
    It should be noted that in simulations conducted with the JuliaGrid package, the accuracy of the results will not be affected by leaving zeros.

---

## [Change Branch Parameters](@id ChangeBranchParametersManual)
The [`parameterBranch!`](@ref parameterBranch!) function is used to modify the parameters of a branch, which include resistance, reactance, susceptance, turns ratio, and shift angle. Although one can modify these parameters by accessing the corresponding variables within the `branch.parameter` field, using the [`parameterBranch!`](@ref parameterBranch!) function will update all affected vectors and matrices, provided that the AC and DC models have been created.

Therefore, this function is useful after executing the [`acModel!`](@ref acModel!) and [`dcModel!`](@ref dcModel!) functions. For example, let us start by creating the AC model:
```@example parameterBranch
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1)
addBus!(system; label = 2)

addBranch!(system; label = 1, from = 1, to = 2, reactance = 0.12, shiftAngle = 0.1745)

acModel!(system)

nothing # hide
```
Then the nodal matrix is:
```@repl parameterBranch
system.acModel.nodalMatrix
```

We can modify the reactance value of the branch and add resistance to it while keeping the shift angle constant, as shown below:
```@example parameterBranch
parameterBranch!(system; label = 1, reactance = 0.10, resistance = 0.02)

nothing # hide
```
Next, we can observe that the nodal matrix is updated automatically by the function:
```@repl parameterBranch
system.acModel.nodalMatrix
```

---

## [Change Generator Status](@id ChangeGeneratorStatusManual)
The [`statusGenerator!`](@ref statusGenerator!) function can be used to change the status of a generator between in-service and out-of-service. It is important to always use this function when changing the status of a previously defined generator. Directly accessing the corresponding variable to change the status can result in incorrect results because it will not affect the variable that holds the active and reactive powers generated by the generators that supply buses.

Therefore, using the function is the safe way to put a generator in or out of service. Let us create the power system with two generators connected to the same bus:
```@example statusGenerator
using JuliaGrid # hide
@default(unit) # hide

system = powerSystem()

addBus!(system; label = 1)
addBus!(system; label = 2)

addGenerator!(system; label = 1, bus = 2, active = 0.4)
addGenerator!(system; label = 2, bus = 2, active = 0.6)

nothing # hide
```

To check the the powers being generated by the generators that supply the buses and outputs of the generators, you can use:
```@repl statusGenerator
system.bus.supply.active
system.generator.output.active
```

To put the second generator out-of-service, you can use the function:
```@example statusGenerator
statusGenerator!(system; label = 2, status = 0)
nothing # hide
```
Here, we can see the updated variables, where the second generator has been taken out of the system:
```@repl statusGenerator
system.bus.supply.active
system.generator.output.active
```
It is worth noting that even if the generator is out-of-service, the `output.active` variable still reflects its production. This is because it allows the same generator to be put back in-service with the same output power as before.

---

## [Change Generator Outputs](@id ChangeGeneratorOutputsManual)
The function [`outputGenerator!`](@ref outputGenerator!) can be utilized to change the output of a generator. Similar to the [Change Generator Status](@ref ChangeGeneratorStatusManual), this function provides the safe way to modify the active and reactive powers produced by the previously defined generator.

To demonstrate how to use this function, we can use the example:
```@example outputGenerator
using JuliaGrid # hide
@default(unit) # hide

system = powerSystem()

addBus!(system; label = 1)
addBus!(system; label = 2)

addGenerator!(system; label = 1, bus = 2, active = 0.5, reactive = 0.1)
addGenerator!(system; label = 2, bus = 2, active = 0.5, reactive = 0.1)

outputGenerator!(system; label = 1, active = 1.0)
nothing # hide
```
In this example, we increase the output active power of the first generator, and the results can be observed below:
```@repl outputGenerator
system.generator.output.active
system.bus.supply.active
```

It is worth noting that the output reactive power of the generators remains the same:
```@repl outputGenerator
system.generator.output.reactive
system.bus.supply.reactive
```

---

## [Add Active and Reactive Costs](@id AddActiveReactiveCostsManual)
The [`addActiveCost!`](@ref addActiveCost!) and [`addReactiveCost!`](@ref addReactiveCost!) functions are responsible for adding costs for the active and reactive power produced by the corresponding generator. These costs are added only if the corresponding generator is defined.

The [`addActiveCost!`](@ref addActiveCost!) function will be used consistently throughout this manual. The same steps can be applied for the [`addReactiveCost!`](@ref addReactiveCost!) function. To begin, let us create an example of the power system using the following code:
```@example addActiveCost
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1)
addBus!(system; label = 2)

addGenerator!(system; label = 1, bus = 2, active = 0.5, reactive = 0.1)

nothing # hide
```

---

##### Polynomial Cost
Let us define a quadratic polynomial cost function ``f(P_g) = 1100P^2_g + 500P_g + 150`` for the active power produced by the generator labelled with 1 using the following code:
```@example addActiveCost
addActiveCost!(system; label = 1, model = 2, polynomial = [1100.0; 500.0; 150.0])
```
As previously, the default input units are related with per-units (pu), and the coefficients of the cost function have units of currency/pu²hr for 1100, currency/puhr for 500, and currency/hr for 150. Hence, the coefficients are stored exactly as entered:
```@repl addActiveCost
system.generator.cost.active.polynomial
```
By setting `model = 2` in the function, we specify that the quadratic polynomial cost is being used for the corresponding generator. This can be particularly useful if we have also defined a piecewise linear cost function for the same generator. In such cases, we can choose between these two cost functions depending on our simulation requirements.

---

##### Piecewise Linear Cost
We can also create a piecewise linear cost function for the active power of the same generator by using:
```@example addActiveCost
addActiveCost!(system; label = 1, piecewise =  [0.1085 12.3; 0.1477 16.8; 0.18 18.1])
nothing # hide
```
In this case, the first column specifies the output active powers of the generator in per-units, while the second column specifies the corresponding costs for the specified active power in currency/hr. Thus, the data is stored exactly as entered:
```@repl addActiveCost
system.generator.cost.active.piecewise
```
If we want to use this piecewise linear cost function instead of the polynomial cost function that was defined earlier, we need to set `model = 1`.

---

##### Change Input Unit System
Changing input units from per-units (pu) can be particularly useful since cost functions are usually related to SI units of powers. To demonstrate this, let us set active powers in megawatts (MW) while keeping the rest of the units in per-units:
```@example addActiveCostUnit
using JuliaGrid # hide
@power(MW, pu, pu)

system = powerSystem()

addBus!(system; label = 1)
addBus!(system; label = 2)

addGenerator!(system; label = 1, bus = 2, active = 50, reactive = 0.1)

nothing # hide
```

Now, we can add the quadratic polynomial function using megawatts:
```@example addActiveCost
addActiveCost!(system; label = 1, model = 2, polynomial = [0.11; 5.0; 150.0])
```
After inspecting the resulting cost data, we can see that it is the same as before:
```@repl addActiveCost
system.generator.cost.active.polynomial
```
Similarly, we can define the linear piecewise cost:
```@example addActiveCost
addActiveCost!(system; label = 1, piecewise =  [10.85 12.3; 14.77 16.8; 18 18.1])
nothing # hide
```
Upon inspection, we can see that the stored data is the same as before:
```@repl addActiveCost
system.generator.cost.active.piecewise
```

---