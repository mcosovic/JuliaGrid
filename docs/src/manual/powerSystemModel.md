# [Power System Model](@id powerSystemModelManual)

The JuliaGrid supports the composite type `PowerSystem` to preserve power system data, with the following fields: `bus`, `branch`, `generator`, `base`, `acModel`, and `dcModel`. The fields `bus`, `branch`, and `generator` hold data related to buses, branches, and generators, respectively. The `base` field stores base values for power and voltages, with the default being three-phase power measured in volt-amperes (VA) for the base power and line-to-line voltages measured in volts (V) for base voltages. The macro command [`@base`](@ref @base) can be used to change the default unit settings for the base quantities of the composite type `PowerSystem` after it is created. 

The composite type `PowerSystem` can be created using a function:
* [`powerSystem`](@ref powerSystem).
JuliaGrid supports three modes for populating the `PowerSystem` type: using built-in functions, using HDF5 file format, and using [Matpower](https://matpower.org) case files. It is recommended to use the HDF5 format for large-scale systems. To facilitate this, JuliaGrid has the function:
* [`savePowerSystem`](@ref savePowerSystem).
This function allows to save power systems that were either loaded from Matpower case files or created using built-in functions in the HDF5 format.

Once the `PowerSystem` type is created, you can add buses, branches, and generators using the following functions:
* [`addBus!`](@ref addBus!)
* [`addBranch!`](@ref addBranch!)
* [`addGenerator!`](@ref addGenerator!).
In addition, it is possible to manipulate the parameters of buses, branches, and generators using the following functions:
* [`shuntBus!`](@ref shuntBus!)
* [`statusBranch!`](@ref statusBranch!)
* [`parameterBranch!`](@ref parameterBranch!)
* [`statusGenerator!`](@ref statusGenerator!)
* [`outputGenerator!`](@ref outputGenerator!).
Executing these functions will automatically update all fields affected by them. You can also change other parameters of the power system by accessing and modifying the values in the `bus`, `branch`, `generator`, and `base` fields of the `PowerSystem` composite type. The input electrical quantities should be entered in per-units or radians, but this default setting can be altered using the following macros [`@power`](@ref @power), [`@voltage`](@ref @voltage), and [`@parameter`](@ref @parameter).

After adding the generator, JualiGrid provides the following functions to incorporate costs for the active and reactive power generated by the generator: 
* [`addActiveCost!`](@ref addActiveCost!)
* [`addReactiveCost!`](@ref addReactiveCost!).

To create vectors and matrices based on the power system topology and parameters, you can use the following functions:
* [`acModel!`](@ref acModel!)
* [`dcModel!`](@ref dcModel!).
Note that these functions can be executed at any time once all power system buses are defined. Specifically, using the [`addBranch!`](@ref addBranch!) function to add a new branch will automatically update the `acModel` and `dcModel` fields. However, adding a new bus using [`addBus!`](@ref addBus!) requires executing the [`acModel!`](@ref acModel!) and [`dcModel!`](@ref dcModel!) functions again. In addition, executing functions related to parameter manipulation of buses and branches will also automatically update the `acModel` and `dcModel` fields.

---

## [Build Model](@id buildModelManual)
The [`powerSystem`](@ref powerSystem) function generates a `PowerSystem` composite type and requires a string-formatted path to either Matpower case or HDF5 files as input. Alternatively, the `PowerSystem` can be generated from scratch using the same function.

---

##### Matpower or HDF5 File  
To create a `PowerSystem` type using the Matpower case file for the IEEE 14-bus test case, which is named `case14` and located in the folder `C:\matpower`, the following Julia code can be used:
```julia-repl
system = powerSystem("C:/matpower/case14.m")
```

You can also use the HDF5 file as input by calling the [`powerSystem`](@ref powerSystem) function, assuming that the file was previously saved using the [`savePowerSystem`](@ref savePowerSystem) function. For example, if the HDF5 file is stored in the directory `C:\matpower`, the following Julia code can be used to construct the `PowerSystem` composite type:
```julia-repl
system = powerSystem("C:/matpower/case14.h5")
```
To minimize loading time, it is recommended to load the power system from the HDF5 file. 

---

##### Model from Scratch
Alternativly, the model can be build from the scratch using built-in functions, for example:
```@example buildModelScratch
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1, base = 138e3)
addBus!(system; label = 2, type = 1, reactive = 0.05, angle = -0.03491, base = 138e3)
addBus!(system; label = 3, type = 1, susceptance = 0.05, base = 138e3)

addBranch!(system; label = 1, from = 1, to = 2, reactance = 0.12, shiftAngle = 0.1745)
addBranch!(system; label = 2, from = 2, to = 3, resistance = 0.008, reactance = 0.05)
```
---

##### Base Units
The `system` stores all its electrical quantities in per-units (pu) and radians (rad), except for the base values of power and voltages. The base power value is expressed in volt-amperes (VA), while the base voltages are given in volts (V). For instance, if you run the previous example, the base power value can be obtained as follows:
```@example buildModelScratch
system.base.power.value, system.base.power.unit
```
Likewise, the base voltages are specified as:
```@example buildModelScratch
system.base.voltage.value, system.base.voltage.unit
```

---

##### Change Base Units
By using the [`@base`](@ref @base) macro, users can change the prefixes of the base units. For instance, if the user wishes to convert base power and base voltage values to megavolt-amperes (MVA) and kilovolts (kV) respectively, they can execute the following command:
```@example buildModelScratch
@base(system, MVA, kV)
nothing # hide
```
After executing the macro, the new base power is:
This macro results in the following values:
```@example buildModelScratch
system.base.power.value, system.base.power.unit
```
Likewise, the updated base voltage values can be retrieved using the following command:
```@example buildModelScratch
system.base.voltage.value, system.base.voltage.unit
```
By modifying the base unit, users can convert the output data from various analyses to units other than per-units.

---

## Save Model
Once the `system` variable has been created using one of the methods outlined in [build model](@ref buildModelManual), the current data can be stored in an HDF5 file by executing the following Julia code:
```julia-repl
savePowerSystem(system; path = "C:/matpower/case14.h5", reference = "IEEE 14-bus test case")
```
All electrical quantities saved in the HDF5 file are in per-units (pu) and radians (rad), except for base values for power and voltage, which are given in volt-amperes (VA) and volts (V). Note that even if the user changes the base units, the units will be saved in the default settings.

---

## Add Bus
We have the option to add buses to a loaded power system or create a new one from scratch. For example, let us create the empty `PowerSystem` type:
```example addBus
system = powerSystem()
```
We can then add buses to this power system using the [`addBus!`](@ref addBus!) function:
```example addBus
addBus!(system; label = 1, type = 3, active = 0.1, base = 138e3)
addBus!(system; label = 2, type = 1, reactive = 0.05, angle = -0.03491, base = 138e3)
```
We recommend reading the documentation for the [`addBus!`](@ref addBus!) function, where all the keywords used in the function are explained in detail.

---

##### Change Input Units
By default, all keywords related to electrical quantities must be given in per-units (pu) and radians (rad). To specify units other than the default per-units and radians, users can utilize macros such as the following:
```example addBus
@power(MW, MVAr, MVA)
@voltage(pu, deg)
```
We can create identical two buses as before using new system of units as follows:
```example addBus
system = powerSystem()

addBus!(system; label = 1, type = 3, active = 10.0, base = 138e3)
addBus!(system; label = 2, type = 1, reactive = 5.0, angle = -2.0, base = 138e3)
```

Note that the `base` keyword is related to base voltages, and the input unit can be changed using the [`@base`](@ref @base) macro. For example:
```example addBus
@power(MW, MVAr, MVA)
@voltage(pu, deg)

system = powerSystem()
@base(system, MVA, kV)

addBus!(system; label = 1, type = 3, active = 10.0, base = 138)
addBus!(system; label = 2, type = 1, reactive = 5.0, angle = -2.0, base = 138)
```

---


## Add Branch
After adding buses with unique labels, we can define branches between them. It is important to note that the branch cannot be added unless the buses are already defined, and the `from` and `to` keywords should correspond to the already defined bus labels. Additionally, each branch should be labeled with its own unique label. For instance:
```example addBranch
system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1, base = 138e3)
addBus!(system; label = 3, type = 1, reactive = 0.05, angle = -0.03491, base = 138e3)

addBranch!(system; label = 1, from = 1, to = 3, reactance = 0.12, shiftAngle = 0.1745)
```
It is recommended to consult the documentation for the [`addBranch!`](@ref addBranch!) function, which provides an explanation of all the keywords used in the function. Similar to buses, the input units for branches can be changed to units other than per-units (pu) and radians (rad) using different macros.

---

## Add Generator
Once the buses are defined, generators can be added to the `system` variable. It is crucial to note that each generator must be assigned a unique label, and the `bus` keyword should match the unique label of the bus to which it is connected. For example:
```example addGenerator
system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1, base = 138e3)
addBus!(system; label = 3, type = 1, reactive = 0.05, angle = -0.03491, base = 138e3)

addGenerator!(system; label = 1, bus = 3, active = 0.5, reactive = 0.1)
```
It is recommended to refer to the documentation for the [`addGenerator!`](@ref addGenerator!) function, which explains all the keywords used in the function. Similar to buses and branches, the input units for branches can be changed to units other than per-units (pu) and radians (rad) using different macros.

---


## AC and DC Model
When constructing the power system, we can create an AC and/or DC model, which include vectors and matrices related to the power system's topology and parameters. The following code snippet demonstrates this:
```@example ACDCModel
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1, base = 138e3)
addBus!(system; label = 2, type = 1, reactive = 0.05, angle = -0.03491, base = 138e3)
addBus!(system; label = 3, type = 1, susceptance = 0.05, base = 138e3)

addBranch!(system; label = 1, from = 1, to = 2, reactance = 0.12, shiftAngle = 0.1745)
addBranch!(system; label = 2, from = 2, to = 3, resistance = 0.008, reactance = 0.05)

acModel!(system)
dcModel!(system)

nothing # hide
```
The AC model is used for AC power flow, AC optimal power flow, nonlinear state estimation or state estimation with PMUs. The DC model is essential for different DC or linear analysis. For example, the DC nodal matrix in the current example is as follows:
```@example ACDCModel
system.dcModel.nodalMatrix
```

---

##### New Branch Triggers Model Update
We can execute the [`acModel!`](@ref acModel!) and [`dcModel!`](@ref dcModel!) functions after defining the final number of buses, and each new branch added will trigger an update of the AC and DC vectors and matrices. Here's an example:
```@example ACDCModelUpdate
using JuliaGrid # hide

system = powerSystem()

addBus!(system; label = 1, type = 3, active = 0.1, base = 138e3)
addBus!(system; label = 2, type = 1, reactive = 0.05, angle = -0.03491, base = 138e3)
addBus!(system; label = 3, type = 1, susceptance = 0.05, base = 138e3)

acModel!(system)
dcModel!(system)

addBranch!(system; label = 1, from = 1, to = 2, reactance = 0.12, shiftAngle = 0.1745)
addBranch!(system; label = 2, from = 2, to = 3, resistance = 0.008, reactance = 0.05)
```
The DC nodal matrix has the same values as before:
```@example ACDCModelUpdate
system.dcModel.nodalMatrix
```

It is not fully recommended to create AC and DC models before adding a large number of branches if the execution time of functions is important. Instead, triggering updates to the AC and DC models using the [`addBranch!`](@ref addBranch!) function is useful for power systems that require the addition of several branches. This update avoids the need to recreate vectors and matrices from scratch.

---

##### New Bus Triggers Model Erasure
It is important to note that the AC and DC models must be defined when a finite number of buses are defined, otherwise, adding a new bus will delete them. For example, if we attempt to add a new bus to the `system` variable, the current DC and AC models will be completely erased.
```@example ACDCModelUpdate
addBus!(system; label = 4, type = 2, base = 138e3)
```
```@example ACDCModelUpdate
system.dcModel.nodalMatrix
```

---

